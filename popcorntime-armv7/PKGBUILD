# Maintainer: Vlad M. <vlad@archlinux.net>
# Contributor: Attila Bukor <r1pp3rj4ck [at] w4it [dot] eu>
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>
# Contributor: xantares <xantares09@hotmail.com>
# Contributor: Eric Engestrom <aur [at] engestrom [dot] ch>
# Contributor: Iwan Timmer <irtimmer@gmail.com>
# Contributor: Ricardo Band <me [at] xengi [dot] de>

# Modifier: gripped forum.odroid.com (credit to Leonard for the build instructions and patched files)

pkgname=popcorntime-armv7
_pkgname=popcorntime
pkgver=0.3.8.3
_pkgver=0.3.8-3
pkgrel=1
pkgdesc="Stream movies from torrents. Skip the downloads. Launch, click, watch."
arch=('armv7')
url="http://popcorntime.io/"
license=('GPL3')
makedepends=('git'
             'nodejs-bower' # install from AUR
             'nodejs-grunt-cli' # install from AUR
             'npm')
depends=('alsa-lib'
         'gconf'
         'gtk2'
         'libnotify'
         'libxtst'
         'nss'
         'ttf-font')
optdepends=('net-tools: vpn.ht client'
            'kodi-xu3: For use as external player set output port to 51234')
options=('!strip')
install="popcorntime.install"
conflicts=('popcorntime-xu3')
replaces=('popcorntime-xu3')
_gitname=desktop.git
_nw_ver=0.12.0

_platform=linuxarm
_nw_platform=linux-arm

source=("desktop-v${_pkgver}.tar.bz2::https://git.popcorntime.io/popcorntime/desktop/repository/archive.tar.bz2?ref=v${_pkgver}"
        "desktop-i18n-master.tar.bz2::https://git.popcorntime.io/popcorntime/desktop-i18n/repository/archive.tar.bz2?ref=master"
        "popcorntime.desktop"
        'arm.patch'
        'kodi.patch'
        'stream.strm'
        'external-kodi-icon.png')
sha256sums=('c8a41e3751439ba2f109f4543511999f0f2b6e8844a3bfea3d864174529a64b8'
            '447bafbdbd06f9e7833732629f333f71b52c78b48dbc0818bfbbc7e2de1e0f47'
            '86d4e736d9fff29fad9892396d1b141c8768db3b80c529391d7b79f4c28c3126'
            '76655d9150e483970a179e5c41ef5035ccf338fa7d33387cfd875dbc61dd26fa'
            'a886118ef0dbb7d0a0e58753456bb353cdf71562410e09b34f56751a8ee9ee60'
            'c3ec78add813aa509e5dc231e04138ac83b2112ea565a40c7506c4de533fc8e4'
            'b37479efe014e4c23e603d1361bb039471e5fdfd24dba6c10078d2393e014fa7')

prepare() {
  cd "${srcdir}/${_gitname}"

  cp "${srcdir}"/desktop-i18n.git/* src/app/language

  export PYTHON=/usr/bin/python2

  npm install
  
  patch --ignore-whitespace -p1 -i ../arm.patch
  patch --ignore-whitespace -p1 -i ../kodi.patch  #enable kodi as external player
  
  cp -v "${srcdir}"/external-kodi-icon.png src/app/images/icons/external-kodi-icon.png
  
#  cp -v "${srcdir}"/Gruntfile.js .
#  cp -v "${srcdir}"/node_webkit_builder.js node_modules/grunt-node-webkit-builder/tasks/node_webkit_builder.js
}

build() {
  cd "${srcdir}/${_gitname}"

  grunt css
  grunt bower_clean
  grunt nodewebkit || grunt nodewebkit
}

package() {
  cd "${srcdir}"

  _bpath="${srcdir}/${_gitname}/build/releases/Popcorn-Time/${_platform}/Popcorn-Time"

  install -dm755 "${pkgdir}/usr/lib/${_pkgname}"
  install -dm755 "${pkgdir}/usr/bin"

  install -Dm755 "${_bpath}/Popcorn-Time" "${pkgdir}/usr/lib/${_pkgname}/"
  install -Dm644 "${_bpath}/nw.pak" "${pkgdir}/usr/lib/${_pkgname}/"
  install -Dm644 "${_bpath}/libffmpegsumo.so" "${pkgdir}/usr/lib/${_pkgname}/"
  install -Dm644 "${srcdir}/${_gitname}/build/cache/${_platform}/${_nw_ver}/icudtl.dat" "${pkgdir}/usr/lib/${_pkgname}/"
  install -Dm644 "stream.strm" "${pkgdir}/usr/lib/${_pkgname}/"

  mkdir -p "${pkgdir}/usr/bin"
  ln -s "/usr/lib/${_pkgname}/Popcorn-Time" "${pkgdir}/usr/bin/${_pkgname}"

  install -Dm644 "${srcdir}/popcorntime.desktop" "${pkgdir}/usr/share/applications/popcorntime.desktop"

  install -Dm644 "${srcdir}/${_gitname}/src/app/images/icon.png" "${pkgdir}/usr/share/pixmaps/popcorntime.png"
}