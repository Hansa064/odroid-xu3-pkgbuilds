# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: kfgz <kfgz at interia pl>
# Modifier:gripped - forum.odroid.com

pkgname=x265-neon
_pkgname=x265
pkgver=1.6
pkgrel=1.1
pkgdesc='Open Source H265/HEVC video encoder'
arch=('i686' 'x86_64')
license=('GPL')
depends=('gcc-libs')
provides=('x265' 'libx265.so')
conflicts=('x265')
makedepends=('yasm' 'cmake')
url='https://bitbucket.org/multicoreware/x265'
source=($_pkgname-$pkgver.tar.bz2::$url/get/$pkgver.tar.bz2
        arm.patch)
md5sums=('d6fcde2d021899bc93f987cd6819a233'
         'SKIP')

prepare() {
  cd multicoreware-x265-*
  patch -p0 -i ../arm.patch
}

build() {
  cd multicoreware-x265-*/build/linux
  [[ $CARCH == x86_64 ]] && LDFLAGS+=',-z,noexecstack'
  cmake -G "Unix Makefiles" -DCMAKE_INSTALL_PREFIX=/usr ../../source
  make
}

package() {
  cd multicoreware-x265-*/build/linux
  make DESTDIR="$pkgdir" install
}
