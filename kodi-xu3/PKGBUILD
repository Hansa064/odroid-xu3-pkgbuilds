# vim:set ts=2 sw=2 et:
# $Id: PKGBUILD 125166 2015-01-02 14:39:41Z idevolder $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: BlackIkeEagle < ike DOT devolder AT gmail DOT com >
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: [vEX] <niechift.dot.vex.at.gmail.dot.com>
# Contributor: Zeqadious <zeqadious.at.gmail.dot.com>
# Contributor: BlackIkeEagle < ike DOT devolder AT gmail DOT com >
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Maxime Gauduin <alucryd@gmail.com>
# Modifier: gripped <forum.odroid.com>

_prefix=/usr

pkgbase=kodi-xu3
pkgname=('kodi-xu3' 'kodi-eventclients-xu3')
pkgver=14.1.802
_codename=Helix
_commit=765bc88fd8a7015b1424fa898849bb1a70211b74
pkgrel=1
arch=('i686' 'x86_64')
url="http://kodi.tv"
license=('GPL2')
makedepends=(
  'afpfs-ng' 'bluez-libs' 'boost' 'cmake' 'curl' 'cwiid' 'doxygen' 'git' 'glew'
  'gperf' 'hicolor-icon-theme' 'jasper' 'java-runtime' 'libaacs' 'libass'
  'libbluray' 'libcdio' 'libcec' 'libgl' 'libmariadbclient' 'libmicrohttpd'
  'libmodplug' 'libmpeg2' 'libnfs' 'libplist' 'libpulse-neon' 'libssh' 'libva'
  'libvdpau' 'libxrandr' 'libxslt' 'lzo' 'nasm' 'nss-mdns' 'python2-pillow'
  'python2-pybluez' 'python2-simplejson' 'rtmpdump' 'sdl2' 'sdl_image'
  'shairplay' 'smbclient' 'swig' 'taglib' 'tinyxml' 'unzip' 'upower' 'yajl' 'zip'
)
source=(
  "$pkgname-$pkgver-$_codename.tar.gz::https://github.com/Owersun/xbmc/archive/$_commit.tar.gz"
  'march.diff'
  '51-cec.rules'
)
sha256sums=(
  'cb8ff7228bd84f2a8b4ddf15e707dc6c76141297480b2efcc27d1c9e9933c137'
  'caff0bc804525a9be02c3d5ae0d93424faa99b56aa597f6c2beb0273a6df3bb7'
  '42d5adfb911e889134f8b81b509fc15a145e4b3e78be9505a46c47a36100e96f'
)

prepare() {
  cd "$srcdir/xbmc-$_commit"
  patch -p0 < ../march.diff
  find -type f -name *.py -exec sed 's|^#!.*python$|#!/usr/bin/python2|' -i "{}" +
  sed 's|^#!.*python$|#!/usr/bin/python2|' -i tools/depends/native/rpl-native/rpl
	sed 's/python/python2/' -i tools/Linux/kodi.sh.in
}

build() {
  cd "$srcdir/xbmc-$_commit"

	# Bootstrapping
  MAKEFLAGS=-j1 ./bootstrap

  #./configure --help
  #return 1

  # Configuring XBMC
  export PYTHON_VERSION=2  # external python v2
  ./configure --prefix=$_prefix --exec-prefix=$_prefix \
    --disable-debug \
    --enable-optimizations \
    --enable-libbluray \
    --enable-texturepacker \
    --with-lirc-device=/run/lirc/lircd \
    --disable-static --enable-shared \
    --disable-rsxs \
    --disable-openmax \
    --disable-vaapi \
    --disable-vdpau \
    --enable-codec=mfc

  # Now (finally) build
  make
}

package_kodi-xu3() {
  pkgdesc="A software media player and entertainment hub for digital media"

  # depends expected for kodi plugins:
  # 'python2-pillow' 'python2-pybluez' 'python2-simplejson'
  # depends expeced in FEH.py
  # 'mesa-demos' 'xorg-xdpyinfo'
  depends=(
    'python2-pillow' 'python2-pybluez' 'python2-simplejson'
    'mesa-demos' 'xorg-xdpyinfo'
    'bluez-libs' 'fribidi' 'glew' 'hicolor-icon-theme' 'libcdio'
    'libjpeg-turbo-neon' 'libmariadbclient' 'libmicrohttpd' 'libpulse-neon' 'libssh'
    'libva' 'libxrandr' 'libxslt' 'lzo' 'sdl2' 'smbclient' 'taglib' 'tinyxml'
    'yajl'
  )
  optdepends=(
    'afpfs-ng: Apple shares support'
    'bluez: Blutooth support'
    'libnfs: NFS shares support'
    'libplist: AirPlay support'
    'libcec: Pulse-Eight USB-CEC adapter support'
    'lirc: Remote controller support'
    'pulseaudio: PulseAudio support'
    'shairplay: AirPlay support'
    'udisks: Automount external drives'
    'unrar: Archives support'
    'unzip: Archives support'
    'upower: Display battery level'
  )
  install="kodi.install"
  provides=('xbmc')
  conflicts=('xbmc' 'xbmc-xu3')
  replaces=('xbmc' 'xbmc-xu3')

  cd "$srcdir/xbmc-$_commit"
  # Running make install
  make DESTDIR="$pkgdir" install

  # Licenses
	install -dm755 ${pkgdir}${_prefix}/share/licenses/${pkgname}
  for licensef in LICENSE.GPL copying.txt; do
		mv ${pkgdir}${_prefix}/share/doc/kodi/${licensef} \
			${pkgdir}${_prefix}/share/licenses/${pkgname}
  done
  

  install -d -m 0755 ${pkgdir}/usr/lib/udev/rules.d
  install -m 0644 ${srcdir}/51-cec.rules ${pkgdir}/usr/lib/udev/rules.d/51-cec.rules
}

package_kodi-eventclients-xu3() {
  pkgdesc="Kodi Event Clients"

  depends=('cwiid')

  cd "$srcdir/xbmc-$_commit"

  make DESTDIR="$pkgdir" eventclients WII_EXTRA_OPTS=-DCWIID_OLD

  install -dm755 "$pkgdir/usr/share/$pkgbase/eventclients"
  mv "$pkgdir/kodi"/* "$pkgdir/usr/share/$pkgbase/eventclients"
  rmdir "$pkgdir/kodi"
}

