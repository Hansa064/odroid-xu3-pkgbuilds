# Maintainer:  prettyvanilla <prettyvanilla@posteo.at>
# Contributor: almostalive   <almostalive2003 at gmail dot com>
# Contributor: Themaister    <maister@archlinux.us>

pkgname=retroarch-git
pkgver=15379.b61ae36
pkgrel=2
pkgdesc="Reference frontend for the libretro API."
arch=('i686' 'x86_64' 'arm' 'armv6h')
url="http://github.com/libretro/RetroArch"
license=('GPL')
# optional build-time deps: 'openal', 'libxml2', 'nvidia-cg-toolkit', 'sdl_image'
# see: 'retroarch --features' for more.
depends=('libgl')
optdepends=('libretro-super-git: A collection of many libretro implementations.')
makedepends=('git')
provides=('retroarch')
conflicts=('retroarch')

_gitname=RetroArch
source=("git+https://github.com/libretro/${_gitname}.git")
md5sums=('SKIP')

pkgver() {
  cd "${_gitname}"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "${_gitname}"
  ./configure --prefix=/usr --enable-udev --enable-threads --disable-ffmpeg --enable-gles --disable-kms \
  --disable-vg --enable-pulse --disable-xvideo --disable-sdl --enable-gles3 \
  --enable-fbo --enable-neon --disable-sdl2 --enable-floathard
  make
}

package() {
  cd "${_gitname}"
  make install DESTDIR="${pkgdir}"
  install -m755 "${srcdir}/RetroArch/tools/retroarch-joyconfig" "${pkgdir}/usr/bin/retroarch-joyconfig"
}
