# ArmSoc (OMAP + Mali)  FrameBuffer driver for Xorg from Linaro.
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>
# Contributor: Erik Andersson <erik.h.andersson@gmail.com>
# Modifier:   gripped forum.odroid.com

# Official upstream is at 
# http://git.linaro.org/gitweb?p=arm/xorg/driver/xf86-video-armsoc.git;a=summary

buildarch=4

pkgname=xf86-video-armsoc-dsd
pkgdesc="armsoc framebuffer driver for X from chromium os git with exynos drm mods"
pkgver=1.1.0.r1.g459fcb4
pkgrel=2
arch=('armv7h')
url="git://anongit.freedesktop.org/xorg/driver/xf86-video-armsoc"
license=('GPL2')
makedepends=('git' 'pkgconfig' 'xorg-server-devel' 'resourceproto' 'scrnsaverproto')
options=('!libtool')
conflicts=('xf86-video-armsoc' 'xf86-video-armsoc-git' 'xf86-video-armsoc-odroid')
source=("$pkgname::git+git://anongit.freedesktop.org/xorg/driver/xf86-video-armsoc"
	'patch.diff')
md5sums=('SKIP'
	'SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
  ( set -o pipefail
    git describe --long --tags 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}
	
build() {
  cd "$srcdir/$pkgname"
  patch -p0 < ../patch.diff
  ./autogen.sh --prefix=/usr --with-drmmode=exynos
  make
}

package() {
  make -C "$srcdir/$pkgname" DESTDIR="${pkgdir}" install
}